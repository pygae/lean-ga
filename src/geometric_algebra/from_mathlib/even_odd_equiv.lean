/-
Copyright (c) 2020 Eric Wieser. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Eric Wieser
-/
import linear_algebra.clifford_algebra.conjugation
import linear_algebra.quadratic_form.prod
import geometric_algebra.from_mathlib.even_odd
/-!
# The isomorphism with the even subalgebra

## Main definitions
* `clifford_algebra.equiv_even`
* `clifford_algebra.even_equiv_even_neg`
-/

namespace clifford_algebra

variables {R M : Type*} [comm_ring R] [add_comm_group M] [module R M]
variables (Q : quadratic_form R M)

namespace equiv_even

/-- The quadratic form on the augmented vector space `M √ó R` sending `v + r‚Ä¢e0` to `Q v - r^2`. -/
@[reducible]
def Q' : quadratic_form R (M √ó R) := (Q.prod $ -@quadratic_form.sq R _)

lemma Q'_apply (m : M √ó R) : Q' Q m = Q m.1 - m.2 * m.2 := (sub_eq_add_neg _ _).symm

/-- The unit vector in the new dimension -/
def e0 : clifford_algebra (Q' Q) := Œπ (Q' Q) (0, 1)

/-- The embedding from the existing vector space -/
def v : M ‚Üí‚Çó[R] clifford_algebra (Q' Q) := (Œπ (Q' Q)) ‚àò‚Çó linear_map.inl _ _ _

lemma Œπ_eq_v_add_smul_e0 (m : M) (r : R) : Œπ (Q' Q) (m, r) = v Q m + r ‚Ä¢ e0 Q :=
by rw [e0, v, linear_map.comp_apply, linear_map.inl_apply, ‚Üêlinear_map.map_smul, prod.smul_mk,
  smul_zero, smul_eq_mul, mul_one, ‚Üêlinear_map.map_add, prod.mk_add_mk, zero_add, add_zero]

lemma e0_mul_e0 : e0 Q * e0 Q = -1 :=
(Œπ_sq_scalar _ _).trans $ by simp

lemma v_sq_scalar (m : M) : v Q m * v Q m = algebra_map _ _ (Q m) :=
(Œπ_sq_scalar _ _).trans $ by simp

lemma neg_e0_mul_v (m : M) : -(e0 Q * v Q m) = v Q m * e0 Q :=
begin
  refine neg_eq_of_add_eq_zero_right ((Œπ_mul_Œπ_add_swap _ _).trans _),
  dsimp [quadratic_form.polar],
  simp only [add_zero, mul_zero, mul_one, zero_add, neg_zero, quadratic_form.map_zero,
    add_sub_cancel, sub_self, map_zero, zero_sub],
end

lemma neg_v_mul_e0 (m : M) : -(v Q m * e0 Q) = e0 Q * v Q m :=
begin
  rw neg_eq_iff_neg_eq,
  exact neg_e0_mul_v _ m
end

@[simp] lemma e0_mul_v_mul_e0 (m : M) : e0 Q * v Q m * e0 Q = v Q m :=
by rw [‚Üêneg_v_mul_e0, ‚Üêneg_mul, mul_assoc, e0_mul_e0, mul_neg_one, neg_neg]

@[simp] lemma reverse_v (m : M) : reverse (v Q m) = v Q m := reverse_Œπ _
@[simp] lemma involute_v (m : M) : involute (v Q m) = -v Q m := involute_Œπ _

@[simp] lemma reverse_e0 : reverse (e0 Q) = e0 Q := reverse_Œπ _
@[simp] lemma involute_e0 : involute (e0 Q) = -e0 Q := involute_Œπ _

end equiv_even

open equiv_even

/-- The embedding from the smaller algebra into the new larger one. -/
def to_even : clifford_algebra Q ‚Üí‚Çê[R] clifford_algebra.even (Q' Q) :=
begin
  refine clifford_algebra.lift Q ‚ü®_, Œª m, _‚ü©,
  { refine linear_map.cod_restrict _ _ (Œª m, submodule.mem_supr_of_mem ‚ü®2, rfl‚ü© _),
    exact (linear_map.mul_left R $ e0 Q).comp (v Q),
    rw [subtype.coe_mk, pow_two],
    exact submodule.mul_mem_mul (linear_map.mem_range_self _ _) (linear_map.mem_range_self _ _), },
  { ext1,
    dsimp only [subalgebra.coe_mul, linear_map.cod_restrict_apply, linear_map.comp_apply,
      linear_map.mul_left_apply, linear_map.inl_apply, subalgebra.coe_algebra_map],
    rw [‚Üêmul_assoc, e0_mul_v_mul_e0, v_sq_scalar] }
end

@[simp]
lemma to_even_Œπ (m : M) : (to_even Q (Œπ Q m) : clifford_algebra (Q' Q)) = e0 Q * v Q m :=
begin
  rw [to_even, clifford_algebra.lift_Œπ_apply, linear_map.cod_restrict_apply],
  refl,
end

/-- The embedding from the even subalgebra with an extra dimension into the original algebra. -/
def of_even : clifford_algebra.even (Q' Q) ‚Üí‚Çê[R] clifford_algebra Q :=
begin
  /-
  Recall that we need:
   * `f ‚ü®0,1‚ü© ‚ü®x,0‚ü© = Œπ x`
   * `f ‚ü®x,0‚ü© ‚ü®0,1‚ü© = -Œπ x`
   * `f ‚ü®x,0‚ü© ‚ü®y,0‚ü© = Œπ x * Œπ y`
   * `f ‚ü®0,1‚ü© ‚ü®0,1‚ü© = -1`
  -/
  let f : (M √ó R) ‚Üí‚Çó[R] (M √ó R) ‚Üí‚Çó[R] clifford_algebra Q :=
  ((algebra.lmul R (clifford_algebra Q)).to_linear_map.comp
    $ ((Œπ Q).comp (linear_map.fst _ _ _)) +
      (algebra.linear_map R _).comp (linear_map.snd _ _ _)).compl‚ÇÇ
    (((Œπ Q).comp (linear_map.fst _ _ _)) - (algebra.linear_map R _).comp (linear_map.snd _ _ _)),
  have f_apply :
    ‚àÄ x y, f x y = (Œπ Q x.1 + algebra_map R _ x.2) * (Œπ Q y.1 - algebra_map R _ y.2) :=
    Œª x y, rfl,
  have hc : ‚àÄ (r : R) (x : clifford_algebra Q), commute (algebra_map _ _ r) x := algebra.commutes,
  have hm : ‚àÄ m : M √ó R,
    Œπ Q m.1 * Œπ Q m.1 - algebra_map R _ m.2 * algebra_map R _ m.2 = algebra_map R _ (Q' Q m),
  { intro m,
    rw [Œπ_sq_scalar, ‚Üêring_hom.map_mul, ‚Üêring_hom.map_sub,
      sub_eq_add_neg, Q'_apply, sub_eq_add_neg] },
  refine even.lift (Q' Q) ‚ü®f, _, _‚ü©; simp_rw [f_apply],
  { intro m,
    rw [‚Üê(hc _ _).symm.mul_self_sub_mul_self_eq, hm] },
  { intros m‚ÇÅ m‚ÇÇ m‚ÇÉ,
    rw [‚Üêmul_smul_comm, ‚Üêmul_assoc, mul_assoc(_ + _), ‚Üê(hc _ _).symm.mul_self_sub_mul_self_eq',
      algebra.smul_def, ‚Üêmul_assoc, hm] },
end

lemma of_even_Œπ (x y : M √ó R) :
  of_even Q (even.Œπ _ x y) = (Œπ Q x.1 + algebra_map R _ x.2) * (Œπ Q y.1 - algebra_map R _ y.2) :=
even.lift_Œπ _ _ _ _

lemma to_even_comp_of_even : (to_even Q).comp (of_even Q) = alg_hom.id R _ :=
even.alg_hom_ext (Q' Q) $ linear_map.ext $ Œª m‚ÇÅ, linear_map.ext $ Œª m‚ÇÇ, subtype.ext $
  let ‚ü®m‚ÇÅ, r‚ÇÅ‚ü© := m‚ÇÅ, ‚ü®m‚ÇÇ, r‚ÇÇ‚ü© := m‚ÇÇ in
  calc  ‚Üë(to_even Q (of_even Q (even.Œπ (Q' Q) (m‚ÇÅ, r‚ÇÅ) (m‚ÇÇ, r‚ÇÇ))))
      = (e0 Q * v Q m‚ÇÅ + algebra_map R _ r‚ÇÅ) * (e0 Q * v Q m‚ÇÇ - algebra_map R _ r‚ÇÇ) :
        by rw [of_even_Œπ, alg_hom.map_mul, alg_hom.map_add, alg_hom.map_sub, alg_hom.commutes,
             alg_hom.commutes, subalgebra.coe_mul, subalgebra.coe_add, subalgebra.coe_sub,
             to_even_Œπ, to_even_Œπ, subalgebra.coe_algebra_map, subalgebra.coe_algebra_map]
  ... = e0 Q * v Q m‚ÇÅ * (e0 Q * v Q m‚ÇÇ) + r‚ÇÅ ‚Ä¢ e0 Q * v Q m‚ÇÇ - r‚ÇÇ ‚Ä¢ e0 Q * v Q m‚ÇÅ
          - algebra_map R _ (r‚ÇÅ * r‚ÇÇ) :
        by rw [mul_sub, add_mul, add_mul, ‚Üêalgebra.commutes, ‚Üêalgebra.smul_def, ‚Üêmap_mul,
               ‚Üêalgebra.smul_def, sub_add_eq_sub_sub, smul_mul_assoc, smul_mul_assoc]
  ... = v Q m‚ÇÅ * v Q m‚ÇÇ + r‚ÇÅ ‚Ä¢ e0 Q * v Q m‚ÇÇ + v Q m‚ÇÅ * r‚ÇÇ ‚Ä¢ e0 Q + (r‚ÇÅ ‚Ä¢ e0 Q) * r‚ÇÇ ‚Ä¢ e0 Q :
        have h1 : e0 Q * v Q m‚ÇÅ * (e0 Q * v Q m‚ÇÇ) = v Q m‚ÇÅ * v Q m‚ÇÇ,
          by rw [‚Üêmul_assoc, e0_mul_v_mul_e0],
        have h2 : -(r‚ÇÇ ‚Ä¢ e0 Q * v Q m‚ÇÅ) = v Q m‚ÇÅ * r‚ÇÇ ‚Ä¢ e0 Q,
          by rw [mul_smul_comm, smul_mul_assoc, ‚Üêsmul_neg, neg_e0_mul_v],
        have h3 : - algebra_map R _ (r‚ÇÅ * r‚ÇÇ) = (r‚ÇÅ ‚Ä¢ e0 Q) * r‚ÇÇ ‚Ä¢ e0 Q,
          by rw [algebra.algebra_map_eq_smul_one, smul_mul_smul, e0_mul_e0, smul_neg],
        by rw [sub_eq_add_neg, sub_eq_add_neg, h1, h2, h3]
  ... = Œπ _ (m‚ÇÅ, r‚ÇÅ) * Œπ _ (m‚ÇÇ, r‚ÇÇ) :
        by rw [Œπ_eq_v_add_smul_e0, Œπ_eq_v_add_smul_e0, mul_add, add_mul, add_mul, add_assoc]

lemma of_even_comp_to_even :
  (of_even Q).comp (to_even Q) = alg_hom.id R _ :=
clifford_algebra.hom_ext $ linear_map.ext $ Œª m,
  calc  of_even Q (to_even Q (Œπ Q m))
      = of_even Q ‚ü®_, (to_even Q (Œπ Q m)).prop‚ü© : by rw subtype.coe_eta
  ... = (Œπ Q 0 + algebra_map R _ 1) * (Œπ Q m - algebra_map R _ 0) : begin
          simp_rw to_even_Œπ,
          exact of_even_Œπ Q _ _,
        end
  ... = Œπ Q m : by rw [map_one, map_zero, map_zero, sub_zero, zero_add, one_mul]

/-- Any clifford algebra is isomorphic to the even subalgebra of a clifford algebra with an extra
dimension (that is, with vector space `M √ó R`), with a quadratic form evaluating to `-1` on that new
basis vector. -/
@[simps]
def equiv_even : clifford_algebra Q ‚âÉ‚Çê[R] clifford_algebra.even (Q' Q) :=
alg_equiv.of_alg_hom
  (to_even Q)
  (of_even Q)
  (to_even_comp_of_even Q)
  (of_even_comp_to_even Q)

/-- The representation of the clifford conjugate (i.e. the reverse of the involute) in the even
subalgebra is just the reverse of the representation. -/
lemma coe_to_even_reverse_involute (x : clifford_algebra Q) :
  ‚Üë(to_even Q (reverse (involute x))) = reverse (to_even Q x : clifford_algebra (Q' Q)) :=
begin
  induction x using clifford_algebra.induction,
  case h_grade0 : r { simp only [alg_hom.commutes, subalgebra.coe_algebra_map, reverse.commutes] },
  case h_grade1 : m {
    simp only [involute_Œπ, subalgebra.coe_neg, to_even_Œπ, reverse.map_mul,
      reverse_v, reverse_e0, reverse_Œπ, neg_e0_mul_v, map_neg] },
  case h_mul : x y hx hy { simp only [map_mul, subalgebra.coe_mul, reverse.map_mul, hx, hy] },
  case h_add : x y hx hy { simp only [map_add, subalgebra.coe_add, hx, hy] },
end

/-- The forward direction of `clifford_algebra.even_equiv_even_neg` -/
def even_to_neg : clifford_algebra.even Q ‚Üí‚Çê[R] clifford_algebra.even (-Q) :=
even.lift Q ‚ü®-(even.Œπ (-Q) : _),
  Œª m, by simp_rw [linear_map.neg_apply, even.Œπ_same, quadratic_form.neg_apply, map_neg, neg_neg],
  Œª m‚ÇÅ m‚ÇÇ m‚ÇÉ, by simp_rw [linear_map.neg_apply, neg_mul_neg, even.Œπ_contract, quadratic_form.neg_apply, smul_neg, neg_smul]‚ü©

@[simp] lemma even_to_neg_Œπ (m‚ÇÅ m‚ÇÇ : M) : even_to_neg Q (even.Œπ Q m‚ÇÅ m‚ÇÇ) = -even.Œπ (-Q) m‚ÇÅ m‚ÇÇ :=
even.lift_Œπ Q _ m‚ÇÅ m‚ÇÇ

/-- The reverse direction of `clifford_algebra.even_equiv_even_neg` -/
def even_of_neg : clifford_algebra.even (-Q) ‚Üí‚Çê[R] clifford_algebra.even Q :=
even.lift (-Q) ‚ü®-(even.Œπ Q : _),
  Œª m, by simp_rw [linear_map.neg_apply, even.Œπ_same, quadratic_form.neg_apply, map_neg],
  Œª m‚ÇÅ m‚ÇÇ m‚ÇÉ, by simp_rw [linear_map.neg_apply, neg_mul_neg, even.Œπ_contract, quadratic_form.neg_apply, neg_smul_neg]‚ü©

@[simp] lemma even_of_neg_Œπ (m‚ÇÅ m‚ÇÇ : M) : even_of_neg Q (even.Œπ (-Q) m‚ÇÅ m‚ÇÇ) = -even.Œπ Q m‚ÇÅ m‚ÇÇ :=
even.lift_Œπ (-Q) _ m‚ÇÅ m‚ÇÇ

/-- The even subalgebra of the algebras with quadratic form `Q` and `-Q` are isomorphic.

Stated another way, `ùíû‚Ñì‚Å∫(p,q,r)` and `ùíû‚Ñì‚Å∫(q,p,r)` are isomorphic. -/
@[simps]
def even_equiv_even_neg : clifford_algebra.even Q ‚âÉ‚Çê[R] clifford_algebra.even (-Q) :=
alg_equiv.of_alg_hom
  (even_to_neg Q)
  (even_of_neg Q)
  (by {
    ext m‚ÇÅ m‚ÇÇ : 3,
    dsimp only [linear_map.compr‚ÇÇ_apply, alg_hom.to_linear_map_apply, alg_hom.comp_apply, alg_hom.id_apply],
    rw [even_of_neg_Œπ, map_neg, even_to_neg_Œπ, neg_neg] })
  (by {
    ext m‚ÇÅ m‚ÇÇ : 3,
    dsimp only [linear_map.compr‚ÇÇ_apply, alg_hom.to_linear_map_apply, alg_hom.comp_apply, alg_hom.id_apply],
    rw [even_to_neg_Œπ, map_neg, even_of_neg_Œπ, neg_neg] })

end clifford_algebra
