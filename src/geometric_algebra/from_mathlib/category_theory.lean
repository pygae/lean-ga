import algebra.category.Algebra.basic
import linear_algebra.clifford_algebra.basic
import geometric_algebra.from_mathlib.basic
import for_mathlib.linear_algebra.quadratic_form.isometric_map

/-! # Category-theoretic interpretations of `clifford_algebra`

## Main definitions

* `QuadraticModule R`: the category of quadratic modules
* `CliffordAlgebra`: the functor from quadratic modules to algebras
* `clifford_algebra.is_initial`: the clifford algebra is initial in the category of pairs
  `(A, clifford_hom Q A)`.

-/

open category_theory
open quadratic_form
open clifford_algebra

universes v u w

set_option old_structure_cmd true

variables (R : Type u) [comm_ring R]

structure QuadraticModule extends Module.{v} R :=
(form' : quadratic_form R carrier)


variables {R}

namespace QuadraticModule

instance : has_coe_to_sort (QuadraticModule.{v} R) (Type v) := ‚ü®QuadraticModule.carrier‚ü©


instance (V : QuadraticModule.{v} R) : add_comm_group V := V.is_add_comm_group
instance (V : QuadraticModule.{v} R) : module R V := V.is_module

def form (V : QuadraticModule.{v} R) : quadratic_form R V := V.form'

instance category : category (QuadraticModule.{v} R) :=
{ hom   := Œª M N, M.form.isometric_map N.form,
  id    := Œª M, isometric_map.id M.form,
  comp  := Œª A B C f g, g.comp f,
  id_comp' := Œª X Y f, isometric_map.to_linear_map_injective $ linear_map.id_comp _,
  comp_id' := Œª X Y f, isometric_map.to_linear_map_injective $ linear_map.comp_id _,
  assoc' := Œª W X Y Z f g h, isometric_map.to_linear_map_injective $ linear_map.comp_assoc _ _ _ }

instance concrete_category : concrete_category.{v} (QuadraticModule.{v} R) :=
{ forget := { obj := Œª R, R, map := Œª R S f, (f : R ‚Üí S) },
  forget_faithful := { } }

instance has_forget_to_Module : has_forget‚ÇÇ (QuadraticModule R) (Module R) :=
{ forget‚ÇÇ :=
  { obj := Œª M, Module.of R M,
    map := Œª M‚ÇÅ M‚ÇÇ f, isometric_map.to_linear_map f } }

instance (M N : QuadraticModule R) : linear_map_class (M ‚ü∂ N) R M N :=
{ coe := Œª f, f,
  .. isometric_map.semilinear_map_class }

end QuadraticModule

/-- The "clifford algebra" functor, sending a quadratic R-module V to the clifford algebra on `V`. -/
@[simps]
def CliffordAlgebra : QuadraticModule.{u} R ‚•§ Algebra.{u} R :=
{ obj := Œª M,
  { carrier := clifford_algebra M.form },
  map := Œª M N f, clifford_algebra.map _ _ f.to_linear_map f.map_app,
  map_id' := by { intros X, ext1, simp only [clifford_algebra.map_comp_Œπ], refl },
  map_comp' := Œª M N P f g, begin
    ext1,
    simp only [clifford_algebra.map_comp_Œπ],
    ext1,
    simp_rw [linear_map.comp_apply, alg_hom.to_linear_map_apply, category_theory.comp_apply,
      clifford_algebra.map_apply_Œπ, isometric_map.coe_to_linear_map],
    simp only [free_algebra.lift_Œπ_apply, category_theory.coe_comp, function.comp_app,
      types_comp_apply]
  end }
.

variables {M : Type w} [add_comm_group M] [module R M] (Q : quadratic_form R M)

/--
The category of pairs of algebras and `clifford_hom`s to those algebras.

https://empg.maths.ed.ac.uk/Activities/Spin/Lecture1.pdf
-/
structure Cliff :=
(alg : Algebra.{v} R)
(hom : clifford_hom Q alg)

namespace Cliff

/-- Convert a `clifford_hom Q A` to an element of `Cliff Q`. -/
def of {A : Type v} [ring A] [algebra R A] (f : clifford_hom Q A) : Cliff.{v} Q :=
{ alg := Algebra.of R A, hom := f } 

instance : category (Cliff Q) :=
{ hom := Œª f g, {h : f.alg ‚ü∂ g.alg // (alg_hom.to_linear_map h).comp f.hom.val = g.hom.val },
  id := Œª f, ‚ü®ùüô _, linear_map.ext $ Œª _, rfl‚ü©,
  comp := Œª x y z f g, ‚ü®f.val ‚â´ g.val,
    by simp only [category_struct.comp, alg_hom.comp_to_linear_map, linear_map.comp_assoc,
      f.property, g.property]‚ü©,
  id_comp' := Œª x y f, subtype.ext $ category.id_comp f,
  comp_id' := Œª x y f, subtype.ext $ category.comp_id f,
  assoc' := Œª w x y z f g h, subtype.ext $ category.assoc f g h }

instance concrete_category : concrete_category.{v} (Cliff.{v} Q) :=
{ forget := { obj := Œª x, x.alg, map := Œª x y f, (forget _).map (subtype.val f : _) },
  forget_faithful := { } }

instance has_forget_to_Algebra : has_forget‚ÇÇ (Cliff Q) (Algebra R) :=
{ forget‚ÇÇ :=
  { obj := Œª x, x.alg,
    map := Œª x y f, f.val } }

instance (Y : Cliff Q) : unique (Cliff.of Q ‚ü®Œπ Q, Œπ_sq_scalar Q‚ü© ‚ü∂ Y) :=
{ default := ‚ü®clifford_algebra.lift Q Y.hom, let ‚ü®A, Œπ, hŒπ‚ü© := Y in Œπ_comp_lift Œπ hŒπ‚ü©,
  uniq := Œª f, subtype.ext begin
    obtain ‚ü®A, Œπ, hŒπ‚ü© := Y,
    exact (clifford_algebra.lift_unique _ hŒπ f.val).mp f.prop,
  end }

end Cliff

/-- The clifford algebra is the initial obect in in `Cliff`. -/
def clifford_algebra.is_initial : limits.is_initial (Cliff.of Q ‚ü®Œπ Q, Œπ_sq_scalar Q‚ü©) :=
limits.is_initial.of_unique _
